#-------------------------------------------------
#
# Project created by QtCreator 2014-12-25T04:49:47
#
#-------------------------------------------------

QT       += core gui

greaterThan(QT_MAJOR_VERSION, 4): QT += widgets

TARGET = test
TEMPLATE = app

LEXSOURCES = gram.l

YACCSOURCES = gram.y

SOURCES += main.cpp\
           base.cpp \
           corefunc.cpp \
        mainwindow.cpp

HEADERS  += mainwindow.h \
    base.h \
    corefunc.h

FORMS    += mainwindow.ui

###########################################################################################
# now we are going to create a Qt compiler ojbect for the EXTRA_COMPILERS FLAG
# this basically has a number of attributes which we need to set as outlined below
###########################################################################################
# this flag list the sources we wish to input to this custom compiler, in this case listed above
# in the FLEXSOURCES variable
lex.input = LEXSOURCES
# now we need to say what files to output, for flex we want to create the .c
lex.output = ${QMAKE_FILE_BASE}.c
# this is the actual compile command we wish to call in this case it wil be
# flex -o (output).c input.l
lex.commands = lex -o ${QMAKE_FILE_IN_BASE}.c ${QMAKE_FILE_IN}
# once this compiler has been run we need to add the output files to the linker stage in this case
# as we are generating .c files we just add it to the SOURCES variable and it will be compiled
lex.variable_out = SOURCES
# this flag ensures that the extra compiler is run before the main c one (as we need to have the .c files
# generated by flex before we use the gcc compiler)
lex.CONFIG += target_predeps
# this tells the make clean command what files to remove
lex.clean =  ${QMAKE_FILE_IN_BASE}.c
# this is just the name of the extra compiler used in the make file (make flex for example)
lex.name = lex
# finally add this to the qmake project so it will have the makefile generated
QMAKE_EXTRA_COMPILERS += lex
# we need the fl lib for flex
LIBS+=-lfl
# tell qt we usign gcc
QMAKE_LINK=g++
